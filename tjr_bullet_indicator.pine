//@version=5
indicator("BPR [TakingProphets]", overlay=true, max_bars_back=500, max_boxes_count=500)

//-----------------------------------------------------------------------------{
// Inputs
//-----------------------------------------------------------------------------{
// General Configuration
show_mitigated = input.bool(true, "Show Mitigated BPR", group="General Configuration")
label_size = input.string("Small", "Label Size", options=["Tiny", "Small", "Normal", "Large"], group="General Configuration")
sensitivity = input.int(2, "Sensitivity", minval=0, maxval=25, group="General Configuration")
show_labels = input.bool(true, "Show Labels", group="General Configuration")

// Midpoint Line
show_midpoint = input.bool(true, "Show Midpoint Line", group="Midpoint Line")
midpoint_color = input.color(color.white, "Midpoint Color", group="Midpoint Line")
midpoint_style_str = input.string("Dashed", "Midpoint Style", options=["Solid", "Dashed", "Dotted"], group="Midpoint Line")
midpoint_width = input.int(1, "Midpoint Width", minval=1, group="Midpoint Line")

// Colors
bearish_bpr_color = input.color(color.new(color.red, 70), "Bearish BPR Color", group="Colors")
bullish_bpr_color = input.color(color.new(color.green, 70), "Bullish BPR Color", group="Colors")

// Calculate text size
var text_size = switch label_size
    "Tiny" => size.tiny
    "Small" => size.small
    "Large" => size.large
    => size.normal

var line bull_midline = na
var line bear_midline = na

var midpoint_style = switch midpoint_style_str
    "Dashed" => line.style_dashed
    "Dotted" => line.style_dotted
    => line.style_solid

var box box_bullish = na
var box box_bearish = na

// FVG Detection with conditions
atr = ta.atr(20)
sens_coeff = sensitivity * 0.08

body_size = math.abs(open - close)
base_cond = (sensitivity == 0 or body_size[1] > (body_size + body_size[2]) * 0.3)

is_bar_bull = close > open
type_cond = true

new_fvg_bearish = base_cond and type_cond and low[2] > high and (low[2] - high) > atr * sens_coeff
new_fvg_bullish = base_cond and type_cond and low > high[2] and (low - high[2]) > atr * sens_coeff

// Bullish BPR
bool new_bull_bpr = false
bool bull_mitigated = false
bull_num_since = ta.barssince(new_fvg_bearish)
if not na(bull_num_since)
    bull_bpr_cond_1 = new_fvg_bullish
    bull_bpr_cond_2 = high[bull_num_since] + low[bull_num_since + 2] + high[2] + low > math.max(low[bull_num_since + 2], low) - math.min(high[bull_num_since], high[2])
    
    if bull_bpr_cond_1 and bull_bpr_cond_2
        new_bull_bpr := true
        bull_combined_low = math.max(high[bull_num_since], high[2])
        bull_combined_high = math.min(low[bull_num_since + 2], low)
        
        if bull_combined_high > bull_combined_low
            if not na(box_bullish)
                box.delete(box_bullish)
            box_bullish := box.new(bar_index - bull_num_since - 1, bull_combined_high, bar_index + 1, bull_combined_low, 
                 border_color=bullish_bpr_color, border_width=1, bgcolor=bullish_bpr_color,
                 text= show_labels ? "BPR" : "", text_size=text_size, text_color=color.white,
                 text_halign=text.align_right, text_valign=text.align_bottom)
            if show_midpoint
                mid = (bull_combined_high + bull_combined_low) / 2
                if not na(bull_midline)
                    line.delete(bull_midline)
                bull_midline := line.new(bar_index - bull_num_since - 1, mid, bar_index + 1, mid, color=midpoint_color, width=midpoint_width, style=midpoint_style)

// Bearish BPR
bool new_bear_bpr = false
bool bear_mitigated = false
bear_num_since = ta.barssince(new_fvg_bullish)
if not na(bear_num_since)
    bear_bpr_cond_1 = new_fvg_bearish
    bear_bpr_cond_2 = high[bear_num_since] + low[bear_num_since + 2] + high[2] + low > math.max(low[bear_num_since + 2], low) - math.min(high[bear_num_since], high[2])
    
    if bear_bpr_cond_1 and bear_bpr_cond_2
        new_bear_bpr := true
        bear_combined_low = math.max(high[bear_num_since + 2], high)
        bear_combined_high = math.min(low[bear_num_since], low[2])
        
        if bear_combined_high > bear_combined_low
            if not na(box_bearish)
                box.delete(box_bearish)
            box_bearish := box.new(bar_index - bear_num_since - 1, bear_combined_high, bar_index + 1, bear_combined_low, 
                 border_color=bearish_bpr_color, border_width=1, bgcolor=bearish_bpr_color,
                 text= show_labels ? "BPR" : "", text_size=text_size, text_color=color.white,
                 text_halign=text.align_right, text_valign=text.align_bottom)
            if show_midpoint
                mid = (bear_combined_high + bear_combined_low) / 2
                if not na(bear_midline)
                    line.delete(bear_midline)
                bear_midline := line.new(bar_index - bear_num_since - 1, mid, bar_index + 1, mid, color=midpoint_color, width=midpoint_width, style=midpoint_style)

// Update existing boxes with invalidation
invalidate_low = math.min(close, open)
invalidate_high = math.max(close, open)

if not na(box_bullish)
    if invalidate_low > box.get_bottom(box_bullish)
        box.set_right(box_bullish, bar_index + 1)
        if show_midpoint and not na(bull_midline)
            line.set_x2(bull_midline, bar_index + 1)
    else if invalidate_low < box.get_bottom(box_bullish)
        bull_mitigated := true
        if not show_mitigated
            box.delete(box_bullish)
            box_bullish := na
            if show_midpoint and not na(bull_midline)
                line.delete(bull_midline)
                bull_midline := na
        else
            box.set_right(box_bullish, bar_index)  // Stop at mitigating candle
            box.set_bgcolor(box_bullish, color.new(bullish_bpr_color, 95))
            box.set_border_color(box_bullish, color.new(bullish_bpr_color, 95))
            if show_midpoint and not na(bull_midline)
                line.set_x2(bull_midline, bar_index)

if not na(box_bearish)
    if invalidate_high < box.get_top(box_bearish)
        box.set_right(box_bearish, bar_index + 1)
        if show_midpoint and not na(bear_midline)
            line.set_x2(bear_midline, bar_index + 1)
    else if invalidate_high > box.get_top(box_bearish)
        bear_mitigated := true
        if not show_mitigated
            box.delete(box_bearish)
            box_bearish := na
            if show_midpoint and not na(bear_midline)
                line.delete(bear_midline)
                bear_midline := na
        else
            box.set_right(box_bearish, bar_index)  // Stop at mitigating candle
            box.set_bgcolor(box_bearish, color.new(bearish_bpr_color, 95))
            box.set_border_color(box_bearish, color.new(bearish_bpr_color, 95))
            if show_midpoint and not na(bear_midline)
                line.set_x2(bear_midline, bar_index)

// Alerts
alertcondition(new_bull_bpr, "Bullish BPR Detected", "New Bullish BPR formed")
alertcondition(new_bear_bpr, "Bearish BPR Detected", "New Bearish BPR formed")
alertcondition(bull_mitigated, "Bullish BPR Mitigated", "Bullish BPR has been mitigated")
alertcondition(bear_mitigated, "Bearish BPR Mitigated", "Bearish BPR has been mitigated")
//-----------------------------------------------------------------------------} 
